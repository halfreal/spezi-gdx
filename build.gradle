plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

allprojects {
    apply plugin: 'maven-publish'

    group = 'de.halfreal'
    version = '1.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        jcenter()

        // Until spezi-mvc is relased in a public repository is needs to be installed in the local
        // maven repository manually.
        mavenLocal()
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
